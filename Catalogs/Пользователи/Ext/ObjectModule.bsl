Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(Наименование);
		
		Если ПользовательИБ = Неопределено Тогда
			ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
			ОбновитьДанныеПользователяИБ(ПользовательИБ);
			
			ИдентификаторПользователяИБ = ПользовательИБ.УникальныйИдентификатор;
			
		Иначе
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст("В списке уже есть пользователь с данным именем!");
			Сообщение.Сообщить();
		КонецЕсли;
		
	Иначе
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ИдентификаторПользователяИБ);
		Если ПользовательИБ <> Неопределено Тогда
			ОбновитьДанныеПользователяИБ(ПользовательИБ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьДанныеПользователяИБ(ПользовательИБ)
	ПользовательИБ.Имя = Наименование;
	ПользовательИБ.ПолноеИмя = ПолноеИмя;
	ПользовательИБ.ПоказыватьВСпискеВыбора = ПоказыватьВСпискеВыбора;
	
	Если ДополнительныеСвойства.Свойство("СписокРолей") Тогда
		ПользовательИБ.Роли.Очистить();
		СписокРолей = ДополнительныеСвойства.СписокРолей;
		
		Для Каждого Роль Из СписокРолей Цикл
			
			Если Роль.Пометка Тогда
				ИмяРоли = Роль.Значение;
				ПользовательИБ.Роли.Добавить(Метаданные.Роли[ИмяРоли]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПользовательИБ.Записать();
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Пользователь = ПользователиИнформационнойБазы.НайтиПоИмени(Наименование);
	
	Если Пользователь = Неопределено Тогда
		Сообщить("Пользователь с данным именем не найден в базе");
		Отказ = Истина;
	Иначе
		Пользователь.Удалить();
		Сообщить("Пользователь " + Наименование + " удален");
	КонецЕсли;
КонецПроцедуры