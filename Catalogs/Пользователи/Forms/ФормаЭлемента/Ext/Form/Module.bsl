
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ТекущийОбъект.ИдентификаторПользователяИБ);
	
	ЗаполнитьСписокРолей(ПользовательИБ);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
		ЗаполнитьСписокРолей();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если РолиИзменены Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("СписокРолей", СписокРолей);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	РолиИзменены = Ложь;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРолей(ПользовательИБ = Неопределено)
	Для Каждого Роль Из Метаданные.Роли Цикл
		РольНазначенаПользователю = ?(ПользовательИБ = Неопределено, Ложь, ПользовательИБ.Роли.Содержит(Роль));
		
		СписокРолей.Добавить(Роль.Имя, Роль.Синоним, РольНазначенаПользователю);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СписокРолейПометкаПриИзменении(Элемент)
	РолиИзменены = Истина;
	Модифицированность = Истина;
КонецПроцедуры
